// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext {
        kotlinVersion = '1.6.10'
        hiltVersion = '2.41'
    }
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:3.1.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id "org.gradle.jacoco"
}

jacoco {
    toolVersion("0.8.7")
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.19.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

task detektAll(type: io.gitlab.arturbosch.detekt.Detekt) {
    description = "Runs detekt analysis over whole code base without the starting overhead for each module."
    parallel = true
    config.setFrom(files("$rootDir/default-detekt-config.yml"))
    setSource(files(projectDir))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
    reports {
        xml.enabled = true
        html.enabled = false
        txt.enabled = false
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: 'buildsystem/jacoco.gradle'
apply from: 'buildsystem/dependencies.gradle'
apply from: 'buildsystem/bitrise.gradle'
apply from: 'buildsystem/secrets.gradle'
